#Использовать fluent
#Использовать fs
#Использовать logos
#Использовать tempfiles

// BSLLS:MissingVariablesDescription-off

Перем Лог;

Перем ТекущийРежимУстановкиПакетов;
Перем КэшУстановленныхПакетов;
Перем ЦелевойКаталогУстановки;
Перем КаталогУстановкиЗависимостей;
Перем КаталогСистемныхБиблиотек;
Перем КешУстановленныхПакетов;
Перем ИмяСервера;

Перем УстанавливатьЗависимости;
Перем УстанавливатьЗависимостиРазработчика;
Перем СоздаватьФайлыЗапуска;

// BSLLS:NumberOfOptionalParams-off
Процедура ПриСозданииОбъекта(Знач ВходящийРежимУстановкиПакетов = Неопределено,
	Знач ВходящийКаталогУстановки = Неопределено,
	Знач ВходящийКаталогУстановкиЗависимостей = Неопределено,
	Знач ВходящийИмяСервера = "")
	
	ПутьККаталогуЛокальнойУстановки = ОбъединитьПути(
		ТекущийКаталог(),
		КонстантыOpm.ЛокальныйКаталогУстановкиПакетов
	);

	Если Не ВходящийРежимУстановкиПакетов = Неопределено Тогда
		УстановитьРежимУстановкиПакетов(ВходящийРежимУстановкиПакетов);
	Иначе
		УстановитьРежимУстановкиПакетов(РежимУстановкиПакетов.Глобально);
	КонецЕсли;

	Если Не ВходящийКаталогУстановки = Неопределено Тогда
		УстановитьЦелевойКаталог(ВходящийКаталогУстановки);
	ИначеЕсли ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Локально Тогда
		УстановитьЦелевойКаталог(ПутьККаталогуЛокальнойУстановки);
	Иначе
		КаталогСистемныхБиблиотек = ПолучитьКаталогСистемныхБиблиотек();
		УстановитьЦелевойКаталог(КаталогСистемныхБиблиотек);
	КонецЕсли;

	Если Не ВходящийКаталогУстановкиЗависимостей = Неопределено Тогда
		УстановитьКаталогЗависимостей(ВходящийКаталогУстановкиЗависимостей);
	Иначе
		Если ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Локально Тогда
			УстановитьКаталогЗависимостей(ПутьККаталогуЛокальнойУстановки);
		ИначеЕсли ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Скачивание Тогда
			УстановитьКаталогЗависимостей(ЦелевойКаталогУстановки);
		Иначе
			КаталогСистемныхБиблиотек = ПолучитьКаталогСистемныхБиблиотек();
			УстановитьКаталогЗависимостей(КаталогСистемныхБиблиотек);
		КонецЕсли;
	КонецЕсли;

	КешУстановленныхПакетов = Новый Соответствие;
	УстанавливатьЗависимости = Истина;
	УстанавливатьЗависимостиРазработчика = Ложь;
	СоздаватьФайлыЗапуска = Истина;
	ИмяСервера = ВходящийИмяСервера;
КонецПроцедуры

Процедура УстанавливатьЗависимости(Знач ПУстанавливатьЗависимости) Экспорт
	УстанавливатьЗависимости = ПУстанавливатьЗависимости;
КонецПроцедуры

Процедура УстанавливатьЗависимостиРазработчика(Знач ПУстанавливатьЗависимостиРазработчика) Экспорт
	УстанавливатьЗависимостиРазработчика = ПУстанавливатьЗависимостиРазработчика;
КонецПроцедуры

Процедура СоздаватьФайлыЗапуска(Знач ПСоздаватьФайлыЗапуска) Экспорт
	СоздаватьФайлыЗапуска = ПСоздаватьФайлыЗапуска;
КонецПроцедуры

Процедура УстановитьРежимУстановкиПакетов(Знач ЗначениеРежимУстановкиПакетов) Экспорт
	
	ТекущийРежимУстановкиПакетов = ЗначениеРежимУстановкиПакетов;

	Если ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Локально Тогда
		ПутьККаталогуЛокальнойУстановки = ОбъединитьПути(
			ТекущийКаталог(),
			КонстантыOpm.ЛокальныйКаталогУстановкиПакетов
		);
		Если КаталогУстановкиЗависимостей = КаталогСистемныхБиблиотек Тогда
			УстановитьКаталогЗависимостей(ПутьККаталогуЛокальнойУстановки);
		КонецЕсли;

	ИначеЕсли ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Скачивание Тогда
		УстановитьКаталогЗависимостей(ЦелевойКаталогУстановки);

	Иначе
		УстановитьКаталогЗависимостей(КаталогСистемныхБиблиотек);
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьЦелевойКаталог(Знач ЗначениеЦелевойКаталогУстановки) Экспорт
	Лог.Отладка("Каталог установки пакета '%1'", ЗначениеЦелевойКаталогУстановки);
	ФС.ОбеспечитьКаталог(ЗначениеЦелевойКаталогУстановки);
	ЦелевойКаталогУстановки = ЗначениеЦелевойКаталогУстановки;
КонецПроцедуры

Процедура УстановитьКаталогЗависимостей(Знач ВыходящийКаталог) Экспорт

	КаталогУстановкиЗависимостей = ВыходящийКаталог;
	
КонецПроцедуры

Процедура УстановитьПакетПоОписанию(Знач ЗависимостьПакета, Знач УровеньЗависимости = 0) Экспорт
	
	Если УровеньЗависимости < 0 Тогда
		ВызватьИсключение "УровеньЗависимости не может быть меньше нуля";
	КонецЕсли;
	
	Если ЗависимостьПакета.ДляРазработки Тогда
		// Зависимости разработчика устанавливаются только на первом уровне (УровеньЗависимости = 0)
		Если УстанавливатьЗависимостиРазработчика И УровеньЗависимости = 0 Тогда
			Лог.Отладка("<%1> отмечена как зависимость для разработчика. Устанавливаем.", ЗависимостьПакета.ИмяПакета);
		Иначе
			Если УровеньЗависимости > 0 Тогда
				Лог.Отладка("<%1> отмечена как зависимость для разработчика, " + 
					"но это транзитивная зависимость (уровень %2). Пропускаем.", ЗависимостьПакета.ИмяПакета, УровеньЗависимости);
			Иначе
				Лог.Отладка("<%1> отмечена как зависимость для разработчика, " + 
					"но установка зависимостей для разработчика не активирована. Пропускаем.", ЗависимостьПакета.ИмяПакета);
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;

	УстановитьПакетПоИмениИВерсии(ЗависимостьПакета.ИмяПакета, ЗависимостьПакета.МинимальнаяВерсия, Истина, УровеньЗависимости);

КонецПроцедуры

Процедура УстановитьПакетИзАрхива(Знач ФайлПакета, Знач ЭтоЗависимыйПакет = Ложь, Знач УровеньЗависимости = 0) Экспорт
	
	Если УровеньЗависимости < 0 Тогда
		ВызватьИсключение "УровеньЗависимости не может быть меньше нуля";
	КонецЕсли;
	
	КаталогУстановки = ?(ЭтоЗависимыйПакет, КаталогУстановкиЗависимостей, ЦелевойКаталогУстановки);
	УстановкаПакета = Новый УстановкаПакета();
	УстановкаПакета.СоздаватьФайлЗапуска(СоздаватьФайлыЗапуска);
	УстановкаПакета.УстановитьЦелевойКаталог(КаталогУстановки);
	Лог.Отладка("КаталогУстановки: %1", КаталогУстановки);
	Если ЭтоЗависимыйПакет Тогда
		УстановкаПакета.УстановитьКешПакетов(КешУстановленныхПакетов);
	КонецЕсли;
	Лог.Отладка("ТекущийРежимУстановкиПакетов: %1", ТекущийРежимУстановкиПакетов);
	УстановкаПакета.УстановитьРежимУстановкиПакета(ТекущийРежимУстановкиПакетов);
	
	Попытка
		УстановкаПакета.УстановитьПакетИзАрхива(ФайлПакета);
	Исключение
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;

	МанифестПакета = УстановкаПакета.ПолучитьМанифестПакета();
	ИмяПакета = МанифестПакета.Свойства().Имя;

	ОбщегоНазначенияOpm.СообщениеПользователю(Лог, "Пакет установлен: %1", ИмяПакета);

	ПолныйПутьККаталогуУстановки = Новый Файл(КаталогУстановки).ПолноеИмя;
	ИмяКаталогаЛокальныхЗависимостей = КонстантыOpm.ЛокальныйКаталогУстановкиПакетов;

	ПутьККаталогуЛокальныхЗависимостей = ОбъединитьПути(ПолныйПутьККаталогуУстановки, ИмяПакета, ИмяКаталогаЛокальныхЗависимостей);
	Если ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Скачивание Тогда
		ПутьККаталогуЛокальныхЗависимостей = ПолныйПутьККаталогуУстановки;
	КонецЕсли;

	Если УстанавливатьЗависимости Тогда
		// Тут надо корректно найти имя пакета в пути
		Если ФС.КаталогСуществует(ПутьККаталогуЛокальныхЗависимостей) Тогда
			РазрешитьЗависимостиПакетаЛокально(МанифестПакета, ПутьККаталогуЛокальныхЗависимостей, УровеньЗависимости);
		Иначе
			РазрешитьЗависимостиПакета(МанифестПакета, УровеньЗависимости);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПакетПоИмениИВерсии(Знач ИмяПакета, Знач ВерсияПакета, ЗНач ЭтоЗависимыйПакет = Ложь, Знач УровеньЗависимости = 0) Экспорт
	
	Если УровеньЗависимости < 0 Тогда
		ВызватьИсключение "УровеньЗависимости не может быть меньше нуля";
	КонецЕсли;
	
	ФайлПакета = РаботаСПакетами.ПолучитьПакет(ИмяПакета, ВерсияПакета, , ИмяСервера);
	УстановитьПакетИзАрхива(ФайлПакета, ЭтоЗависимыйПакет, УровеньЗависимости);

КонецПроцедуры

Процедура РазрешитьЗависимостиПакета(Знач Манифест, Знач УровеньЗависимости = 0) Экспорт
	
	Если УровеньЗависимости < 0 Тогда
		ВызватьИсключение "УровеньЗависимости не может быть меньше нуля";
	КонецЕсли;
	
	ВсеЗависимости = Манифест.Зависимости();
	Если ВсеЗависимости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Фильтруем зависимости в зависимости от флага УстанавливатьЗависимостиРазработчика
	// Зависимости разработчика устанавливаются только на первом уровне (УровеньЗависимости = 0)
	Отбор = Новый Структура();
	Если Не (УстанавливатьЗависимостиРазработчика И УровеньЗависимости = 0) Тогда
		Отбор.Вставить("ДляРазработки", Ложь);
	КонецЕсли;
	
	Зависимости = ВсеЗависимости.НайтиСтроки(Отбор);
	
	Для Каждого Зависимость Из Зависимости Цикл
		Лог.Отладка("Включена зависимость: %1 (ДляРазработки: %2)", Зависимость.ИмяПакета, Зависимость.ДляРазработки);
	КонецЦикла;
	
	УстановленныеПакеты = ПолучитьУстановленныеПакеты();

	Для Каждого Зависимость Из Зависимости Цикл
		
		Лог.Отладка("Требуется зависимость: " + Зависимость.ИмяПакета);

		Если Не УстановленныеПакеты.ПакетУстановлен(Зависимость, КаталогУстановкиЗависимостей) Тогда
			
			Лог.Отладка("Устанавливаю зависимость: %1", Зависимость.ИмяПакета);

			// скачать
			// определить зависимости и так по кругу
			УстановитьПакетПоОписанию(Зависимость, УровеньЗависимости + 1);
			УстановленныеПакеты.Обновить();

		Иначе
			Лог.Отладка("" + Зависимость.ИмяПакета + " уже установлен. Пропускаем.");
			// считаем, что версия всегда подходит
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РазрешитьЗависимостиПакетаЛокально(Манифест, ПутьККаталогуЛокальныхЗависимостей, Знач УровеньЗависимости = 0)
	
	ВсеЗависимости = Манифест.Зависимости();
	Если ВсеЗависимости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Фильтруем зависимости в зависимости от флага УстанавливатьЗависимостиРазработчика
	// Зависимости разработчика устанавливаются только на первом уровне (УровеньЗависимости = 0)
	Отбор = Новый Структура();
	Если Не (УстанавливатьЗависимостиРазработчика И УровеньЗависимости = 0) Тогда
		Отбор.Вставить("ДляРазработки", Ложь);
	КонецЕсли;
	
	Зависимости = ВсеЗависимости.НайтиСтроки(Отбор);
	
	Для Каждого Зависимость Из Зависимости Цикл
		Лог.Отладка("Включена зависимость: %1 (ДляРазработки: %2)", Зависимость.ИмяПакета, Зависимость.ДляРазработки);
	КонецЦикла;
	
	УстановленныеПакеты = ПолучитьУстановленныеПакеты();

	Для Каждого Зависимость Из Зависимости Цикл
		
		Лог.Отладка("Устанавливаю зависимость: <%1> из каталога локальных зависимостей", Зависимость.ИмяПакета);

		Если УстановленныеПакеты.ПакетУстановлен(Зависимость, КаталогУстановкиЗависимостей) Тогда
			Лог.Отладка("<%1> уже установлен. Пропускаем.", Зависимость.ИмяПакета);
			Продолжить;
		КонецЕсли;
		
		ПутьККаталогуПакетаЗависимостиИсточник = ОбъединитьПути(ПутьККаталогуЛокальныхЗависимостей, Зависимость.ИмяПакета);

		Если ТекущийРежимУстановкиПакетов <> РежимУстановкиПакетов.Скачивание
			И ФС.КаталогСуществует(ПутьККаталогуПакетаЗависимостиИсточник) Тогда

			ПутьККаталогуПакетаЗависимостиПриемник = ОбъединитьПути(КаталогУстановкиЗависимостей, Зависимость.ИмяПакета);
			ФС.КопироватьСодержимоеКаталога(ПутьККаталогуПакетаЗависимостиИсточник, ПутьККаталогуПакетаЗависимостиПриемник);
		
		Иначе

			УстановитьПакетПоОписанию(Зависимость, УровеньЗависимости + 1);
		
		КонецЕсли;

		УстановленныеПакеты.Обновить();

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьУстановленныеПакеты()
	
	Если ТекущийРежимУстановкиПакетов = РежимУстановкиПакетов.Скачивание Тогда
		КэшУстановленныхПакетов = Новый КэшУстановленныхПакетов(ЦелевойКаталогУстановки);
	Иначе
		КэшУстановленныхПакетов = Новый КэшУстановленныхПакетов(КаталогУстановкиЗависимостей);
	КонецЕсли;

	Возврат КэшУстановленныхПакетов;

КонецФункции

Функция ПолучитьКаталогСистемныхБиблиотек()
	
	СистемныеБиблиотеки = ОбъединитьПути(КаталогПрограммы(), ПолучитьЗначениеСистемнойНастройки("lib.system"));
	Лог.Отладка("СистемныеБиблиотеки %1", СистемныеБиблиотеки);
	Если СистемныеБиблиотеки = Неопределено Тогда
		ВызватьИсключение "Не определен каталог системных библиотек";
	КонецЕсли;
	
	Возврат СистемныеБиблиотеки;
	
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.app.opm.install");

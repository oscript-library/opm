
#Использовать json
#Использовать logos

Перем мНастройки;
Перем мПутьФайлаНастроек;
Перем Лог;

Процедура УстановитьФайлНастроек(Знач Путь) Экспорт

	мПутьФайлаНастроек = Путь;
	мНастройки = Неопределено;

КонецПроцедуры

Функция Получить() Экспорт

	Если мНастройки = Неопределено Тогда
		Попытка
			ПрочитатьФайлНастроек();
		Исключение
			Лог.Отладка("Чтение файла настроек:
				|" + ОписаниеОшибки());
			УстановитьНастройкиПоУмолчанию();
		КонецПопытки;
	КонецЕсли;

	Возврат мНастройки;

КонецФункции

Процедура ПрочитатьФайлНастроек()
	
	Если Не ЗначениеЗаполнено(мПутьФайлаНастроек) Тогда
		ВызватьИсключение "Не установлен файл настроек";
	КонецЕсли;

	Текст = ПрочитатьФайл(мПутьФайлаНастроек);

	Чтение = Новый ПарсерJSON;
	Настройки = Чтение.ПрочитатьJSON(Текст,,,Истина);

	// TODO сделать конвертацию терминов json в русские свойства настроек

	мНастройки = Настройки;

КонецПроцедуры

Функция ПрочитатьФайл(Знач Путь)

	Чтение = Новый ЧтениеТекста(Путь);
	Текст = Чтение.Прочитать();
	Чтение.Закрыть();

	Возврат Текст;

КонецФункции

Процедура СохранитьФайл(Знач Текст,Знач Путь)
 
 	Запись = Новый ЗаписьТекста(Путь);
 	Запись.ЗаписатьСтроку(Текст);
 	Запись.Закрыть();
 
 КонецПроцедуры

Процедура УстановитьНастройкиПоУмолчанию()
	мНастройки = Новый Структура;
	НастройкиПроксиПоУмолчанию = НастройкиПроксиПроксиПоУмолчанию();
	мНастройки.Вставить("Прокси", НастройкиПроксиПоУмолчанию);
	мНастройки.Вставить("СоздаватьShСкриптЗапуска", Ложь);

КонецПроцедуры

Функция НастройкиПроксиПроксиПоУмолчанию()

	СтруктураПрокси = Новый Структура();
	СтруктураПрокси.Вставить("ИспользоватьПрокси", Ложь);
	СтруктураПрокси.Вставить("ПроксиПоУмолчанию", Истина);
	СтруктураПрокси.Вставить("Сервер");
	СтруктураПрокси.Вставить("Порт");
	СтруктураПрокси.Вставить("Пользователь");
	СтруктураПрокси.Вставить("Пароль");
	СтруктураПрокси.Вставить("ИспользоватьАутентификациюОС",Ложь);

	Возврат СтруктураПрокси;
КонецФункции	

Процедура СохранитьНастройки(Знач Параметры) Экспорт
	Получить();
	ЗаполнитьНастройкиИзПараметров(Параметры);
	Текст = СформироватьТекстНастроек(мНастройки);
	СохранитьФайл(Текст,мПутьФайлаНастроек);
КонецПроцедуры
  
Функция СформироватьТекстНастроек(Знач Настройки)
	ТекстНастроек = "";
	Json          =  Новый ПарсерJSON;
	ТекстНастроек = Json.ЗаписатьJSON(Настройки);

	Возврат ТекстНастроек;
КонецФункции

Процедура ЗаполнитьНастройкиИзПараметров(знач ЗначенияПараметров)
	
	мНастройки.Прокси.ПроксиПоУмолчанию = НЕ ЗначенияПараметров["-proxyusedefault"] = Неопределено;
	мНастройки.Прокси.Сервер            = ?(ЗначенияПараметров["-proxyserver"]      = Неопределено,	мНастройки.Прокси.Сервер,       ЗначенияПараметров["-proxyserver"]);
	мНастройки.Прокси.Порт              = ?(ЗначенияПараметров["-proxyport"]        = Неопределено,	мНастройки.Прокси.Порт,         ЗначенияПараметров["-proxyport"]);
	мНастройки.Прокси.Пользователь      = ?(ЗначенияПараметров["-proxyuser"]        = Неопределено,	мНастройки.Прокси.Пользователь, ЗначенияПараметров["-proxyuser"]);
	мНастройки.Прокси.Пароль            = ?(ЗначенияПараметров["-proxypass"]        = Неопределено,	мНастройки.Прокси.Пароль,       ЗначенияПараметров["-proxypass"]);
	
	Если мНастройки.Прокси.ПроксиПоУмолчанию Тогда
			мНастройки.Прокси.Сервер       = "";
			мНастройки.Прокси.Порт         = "";
			мНастройки.Прокси.Пользователь = "";
			мНастройки.Прокси.Пароль       = "";
	КонецЕсли;	

	мНастройки.Прокси.ИспользоватьПрокси =  мНастройки.Прокси.ПроксиПоУмолчанию ИЛИ ЗначениеЗаполнено(мНастройки.Прокси.Сервер);

	мНастройки.СоздаватьShСкриптЗапуска = ?(
		ЗначенияПараметров["-winCreateBashLauncher"] = Неопределено,
		мНастройки.СоздаватьShСкриптЗапуска,
		Булево(ЗначенияПараметров["-winCreateBashLauncher"])
	);

	СерверУдаленногоХранилища = ЗначенияПараметров["-hub-url"];

	Если ЗначениеЗаполнено(СерверУдаленногоХранилища) Тогда   
		мНастройки.СерверУдаленногоХранилища = СерверУдаленногоХранилища;
	КонецЕсли;

	ПутьВХранилище = ЗначенияПараметров["-hub-path"];
	
	Если ЗначениеЗаполнено(ПутьВХранилище) Тогда   
		мНастройки.ПутьВХранилище = ПутьВХранилище;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает имя глобальной переменой сервера удаленного хранилища 
// 
Функция ИмяПеременнойСервераУдаленногоХранилища()

	Возврат "OPM_HUB_URL";

КонецФункции

// Функция возвращает установленные значение в глобальной переменой 
// 
Функция ПолучитьЗначениеГлобальнойПеременноСервераУдаленногоХранилища()
	
	СИ = Новый СистемнаяИнформация;
	Возврат СИ.ПолучитьПеременнуюСреды(ИмяПеременнойСервераУдаленногоХранилища());

КонецФункции // ПолучитьЗначениеГлобальнойПеременноСервераУдаленногоХранилища()

// Функция возвращает установленные настройки сервера удаленного хранилища
//  
// Возвращаемое значение:
//   Структура - Список настроек.
//		Ключ - ключ настройки
//		Значение - значение настройки
//
Функция НастройкаСервераУдаленногоХранилища() Экспорт

	Настройка = Новый Структура("СерверУдаленногоХранилища, ПутьВХранилище");
	
	// Логика 
	// 1. Получение из глобальной переменной 
	// 2. Получение из файла настроект 
	// 3. Получение констант
	//
	ЗначениеИзГлабоальнойПеремненой = ПолучитьЗначениеГлобальнойПеременноСервераУдаленногоХранилища();
	НастройкаИзФайлаНастроек = Получить();
		

	Если ЗначениеЗаполнено(ЗначениеИзГлабоальнойПеремненой) Тогда
		// В глобальной переменной хранится весь путь из него нам надо получить протокол, сервер и адрес на сервере
		// Получение из URL строк сервера и адреса на сервера
		// (?:([^:/?#]+):)?(?://([^/?#]*))?([^?#]*)(?:\?([^#]*))?(?:#(.*))?
		// Группа 1 - протокол
		// 	      2 - сервера
		//        3 - адрес на сервере
		//
		РегуляркаДляURL =  Новый РегулярноеВыражение ("(?:([^:/?#]+):)?(?://([^/?#]*))?([^?#]*)(?:\?([^#]*))?(?:#(.*))?");
     	КоллекцияСовпадений = РегуляркаДляURL.НайтиСовпадения(ЗначениеИзГлабоальнойПеремненой);
		
		Настройка.СерверУдаленногоХранилища = СтрШаблон("%1://%2", КоллекцияСовпадений[0].Группы[1].Значение, КоллекцияСовпадений[0].Группы[2].Значение);
		Настройка.ПутьВХранилище = КоллекцияСовпадений[0].Группы[3].Значение;

	ИначеЕсли НастройкаИзФайлаНастроек.Свойство("СерверУдаленногоХранилища") Тогда
			
		Настройка.СерверУдаленногоХранилищ = НастройкаИзФайлаНастроек.СерверУдаленногоХранилищ;
		Настройка.ПутьВХранилище = НастройкаИзФайлаНастроек.ПутьВХранилище;
	
	Иначе
			
		Настройка.СерверУдаленногоХранилищ = Константы.СерверУдаленногоХранилища;
		Настройка.ПутьВХранилище = Константы.ПутьВХранилище;
	
	КонецЕсли;


	Возврат Настройка;
	
КонецФункции // НастройкаСервераУдаленногоХранилища()



//------------

Лог = Логирование.ПолучитьЛог("oscript.app.opm");